#!/usr/bin/perl
#
# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 2 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software
# Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA
#
#

use strict;
use warnings;

use Audio::MPD;
use Getopt::Euclid qw[ :minimal_keys ];
use Proc::Daemon;
use Time::HiRes    qw[ usleep ];

Proc::Daemon::Init unless $ARGV{debug};

#
my $song     = 0; # song currently playing
my $playlist = 0; # playlist version
my $mpd = Audio::MPD->new;

# fetch list of songs known by mpd.
my @files = $mpd->collection->all_pathes;


while (1) { # endless loop
    my $status;
    eval { $status = $mpd->status };
    next if $@; # error while peaking status

    # do playlist and/or current song have changed?
    next unless $status->playlist > $playlist
        || defined $status->song && $status->song != $song;
    debug("need to check playlist...\n");

    # yup - update playlist & song.
    $playlist = $status->playlist;
    $song     = $status->song;

    # keep at most $ARGV{old} songs.
    if ( $song > $ARGV{old} ) {
        my $old = $song - $ARGV{old};
        eval { $mpd->delete(0) for 1..$old };
    }

    # add at most $ARGV{new} songs.
    my $pl = $mpd->playlist;
    if ( $#$pl - $song < $ARGV{new} ) {
        my $new = $ARGV{new} - ( $#$pl - $song );
        eval { $mpd->add( $files[ rand @files ] ) for 1..$new };
    }

} continue {
    usleep $ARGV{sleep} * 1000 * 1000;
}

exit; # should not be there...

sub debug {
    return unless $ARGV{debug};
    my ($msg) = @_;
    my ($s,$m,$h) = ( localtime(time) )[0,1,2,3,6];
    my $date = sprintf "%02d:%02d:%02d", $h, $m, $s;
    warn "$date $msg";
}

__END__


=head1 NAME

mpd-dynamic - a dynamic playlist for mpd


=head1 USAGE

    mpd-dynamic


=head1 VERSION

This is mpd-dynamic version 0.2


=head1 DESCRIPTION

This program implements a dynamic playlist for mpd.

MPD (music player daemon) is a cool music player, but it lacks a dynamic
playlist. A dynamic playlist is a playlist that will change automatically
over time.

In particular, it will remove already played songs (keeping at most a given
number) and add new songs to the playlist so it never fall short of songs.

Note that since mpd is a daemon needing no gui to work, C<mpd-dynamic> is
also a daemon. That is, it will fork and do all its work from the background.
This way, you can fire C<mpd> and C<mpd-dynamic> and forget completely
about your music (especially since C<mpd-dynamic> is a low-resource program):
it will just be there! :-)


=head1 OPTIONS

=item -c[onf] <file>

Path of configuration file. Defaults to C<~/.mpd/mpd-dynamic.conf>.

=for Euclid:
    file.type:     readable
    file.default:  "$ENV{HOME}/.mpd/mpd-dynamic.conf"


=item -o[ld] <old>

Number of old tracks to keep in the backlog. Defaults to 10.

=for Euclid:
    old.type:     integer >= 0
    old.default:  10


=item -n[ew] <new>

Number of new tracks to keep in the to-be-played playlist. Defaults to 10.

=for Euclid:
    new.type:     integer > 0
    new.default:  10


=item -s[leep] <sleep>

Time spent sleeping (in seconds) before checking if playlist should be updated.

=for Euclid:
    sleep.type:     number > 0
    sleep.default:  5


=item -d[ebug]

Run mpd-dynamic in debug mode. In particular, the program will not daemonize
itself.


=head1 AUTHOR

Jerome Quelin <jquelin@cpan.org>


=head1 COPYRIGHT

Copyright (c) 2007 Jerome Quelin <jquelin@cpan.org>

This program is free software; you can redistribute it and/or
modify it under the same terms as Perl itself.

